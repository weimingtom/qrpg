///////////////////////////////////////////////////////////
//  Act.as
//  Macromedia ActionScript Implementation of the Class Act
//  Generated by Enterprise Architect
//  Created on:      12-六月-2009 22:54:20
//  Original author: 陈策
///////////////////////////////////////////////////////////

package qrpg.action
{
	/**
	 * @author 陈策
	 * @version 3.0
	 * @created 12-六月-2009 22:54:20
	 */
	public class Act
	{
		/**
		 * 动作名字。
		 */
		public var name: String;
		
		/**
		 * 动作类型。
		 */
		public var type: String;
		
		//该动作包含的所有帧。
		private var _frames:Vector.<Frame>;
		
		/**
		 * 动作。
		 * @param name 动作名。
		 * @param type 动作类型。
		 */		
		public function Act(name:String, type:String)
		{
			this.name = name;
			this.type = type;
			_frames = new Vector.<Frame>();
		}
		
		/**
		 * 动作的帧数。
		 */		
		public function get length():int
		{
			return _frames.length;
		}
		
		/**
		 * 通过XML生成动作。
		 * @param xml 生成动作的XML。
		 * @return 动作。
		 */		
		public static function createByXML(xml:XML):Act
		{
			//TODO XML格式未定，以后定下再写。
			return null;
		}
		
		/**
		 * 返回代表这个动作的XML。
		 * @return 代表这个动作的XML。
		 */		
		public function getXML():XML
		{
			//TODO XML格式未定，以后定下再写。
			return null;
		}
	
		/**
		 * 将帧添加至动作，做为该动作的最后一帧。
		 * @param frame 要添加的帧。
		 * @return 添加后总帧数。
		 */
		public function addFrame(frame:Frame):int
		{
			_frames.push(frame);
			return _frames.length;
		}
	
		/**
		 * 将帧添加到动作的指定位置。
		 * @param frame 要添加的帧。
		 * @param index 要添加帧至指定的位置。
		 * @return 添加后总帧数。
		 */
		public function addFrameAt(frame:Frame, index:int):int
		{
			_frames.splice(index, 0, frame);
			return _frames.length;
		}
		
		/**
		 * 得到指定位置的帧。
		 * @param index 指定的位置。
		 * @return 指定的帧。
		 */		
		public function getFrameAt(index:int):Frame
		{
			if ( index<0 || index>=length )
			{
				return _frames[0];
			}
			return _frames[index];
		}
	
		/**
		 * 删除指定的帧。
		 * @param frame 要删除的帧。
		 * @return 被删除的帧。
		 */
		public function removeFrame(frame:Frame):Frame
		{
			var remove:Frame;
			var i:int;
			var len:int = _frames.length;
			for ( i=0; i<len; i++ )
			{
				if ( _frames[i]==frame )
				{
					remove = _frames.splice(i,1)[0];
					break;
				}
			}
			return remove;
		}
	
		/**
		 * 删除指定位置的帧。
		 * @param index 删除指定位置的帧。
		 * @return 被删除的帧。
		 */
		public function removeFrameAt(index:int):Frame
		{
			var remove:Frame = _frames.splice(index,1)[0];
			return remove;
		}
		
		public function toString():String
		{
			return "[Act size:"+length+"]";
		}

	}
}