///////////////////////////////////////////////////////////
//  BmpObject.as
//  Macromedia ActionScript Implementation of the Class BmpObject
//  Generated by Enterprise Architect
//  Created on:      12-六月-2009 22:54:20
//  Original author: 陈策
///////////////////////////////////////////////////////////

package qrpg.display
{
	import flash.display.Bitmap;
	import flash.display.BitmapData;
	import flash.display.DisplayObject;
	import flash.display.Loader;
	import flash.display.LoaderInfo;
	import flash.events.Event;
	import flash.events.ProgressEvent;
	import flash.geom.Matrix;
	import flash.geom.Point;
	import flash.geom.Rectangle;
	import flash.net.URLRequest;
	
	import qrpg.action.Act;
	import qrpg.action.Actions;
	import qrpg.event.GameEvent;
	
	/**
	 * 加载过程中。
	 * @EventType flash.events.ProgressEvent.PROGRESS
	 */	
	[Event(name="progress", type="flash.events.ProgressEvent")]
	
	/**
	 * 加载完成。
	 * @EventType flash.events.Event.COMPLETE
	 */	
	[Event(name="complete", type="flash.events.Event")]
	
	/**
	 * 显示需要更新。
	 * @EventType qrpg.event.GameEvent.UPDATE
	 */	
	[Event(name="update", type="qrpg.event.GameEvent")]
	
	/**
	 * 显示物体的基类。
	 * 该类不是DisplayObject的子类，所以是不可以添加至显示列表里的。<br/>
	 * 要显示该类的实例，需要用该类的displayData()方法得到需要显示的图像的BitmapData。
	 * @author 陈策
	 * @version 3.0
	 * @created 12-六月-2009 22:54:20
	 */
	public class BmpObject extends Bitmap implements ISceneAddable
	{
		private var _actions:Actions;				//物体动作集。
		protected var _source:DisplayObject;		//物体显示可能用的显示对象。
		protected var _url:URLRequest;				//物体的图片或者显示对象可能的加载路径。
		protected var _mirrorBitmapData:BitmapData;	//如果有镜像侦，则会存在这里。
		
		protected var _isUpdate:Boolean;			//是否需要更新。
		protected var _isLoaded:Boolean;			//是否已经加载。
		
		/**
		 * 显示物体的基类。
		 */		
		public function BmpObject()
		{
			super();
			_isUpdate = false;
			_isLoaded = false;
			addEventListener(Event.ENTER_FRAME, onEnterFrame);
		}
		
		/**
		 * 图片资源。该属性可以是字符，可以是类，可以是显示对象。
		 * 如果该属性是字符串，则会加载该字符串所代表的URL的资源。
		 * 如果是类，则会实例化为一个显示对象。
		 * 如果为一个显示对象，则为需要显示的资源。
		 * @param obj 数据源。
		 */
		public function set source(obj:Object):void
		{
			if ( obj is BitmapData )
			{
				bitmapData = obj as BitmapData;
				_isLoaded = true;
				checkMirror();
			}
			else if ( obj is Bitmap )
			{
				bitmapData = (obj as Bitmap).bitmapData;
				_isLoaded = true;
				checkMirror();
			}
			else if ( obj is DisplayObject )
			{
				_source = obj as DisplayObject;
				_isLoaded = true;
			}
			else if ( obj is Class )
			{
				var tmp:* = new obj();
				if ( tmp is Bitmap )
				{
					bitmapData = (tmp as Bitmap).bitmapData;
					checkMirror();
				}
				else if ( tmp is DisplayObject )
				{
					_source = tmp as DisplayObject;
				}
				else
				{
					throw new Error("参数类型不正确。");
				}
				_isLoaded = true;
			}
			else if ( obj is String || obj is URLRequest )
			{
				if ( obj is String ) _url = new URLRequest(String(obj));
				else _url = obj as URLRequest;
				load();
			}
		}
		
		/**
		 * 物体的动作集。
		 */
		public function get actions():Actions
		{
			return _actions;
		}
		public function set actions(acts:Actions):void
		{
			_actions = acts;
			checkMirror();
		}
		
		/**
		 * 是否已经更新。<br/>
		 * 注意：该属性是检查从上回一检查该属性到这回检查为止是否有更新。<br/>
		 * 所以如果连联调用两次该属性，第二次的结果一定会是false。
		 */		
		public function get isUpdate():Boolean
		{
			var value:Boolean = _isUpdate;
			_isUpdate = false;
			return value;
		}
		public function set isUpdate(value:Boolean):void
		{
			_isUpdate = value;
			if ( _isUpdate ) dispatchEvent(new GameEvent(GameEvent.UPDATE));
		}
		
		/**
		 * 是否已经加载。
		 */		
		public function get isLoaded():Boolean
		{
			return _isLoaded;
		}
		
		/**
		 * 显示的中心。
		 */		
		public function get displayPoint():Point
		{
			var p:Point = new Point();
			if ( !isMirror )
			{
				p.x = -actions.currentFrame.center.x;
			}
			else
			{
				p.x = actions.currentFrame.center.x - actions.currentFrame.rect.width;
			}
			p.y = -actions.currentFrame.center.y;
			return p;
		}
		
		/**
		 * 目前该对象的显示是否为水平镜像显示
		 * @return 
		 */		
		public function get isMirror():Boolean
		{
			return _actions.currentFrame.isMirror;
		}
		
		/**
		 * 是否被显示。
		 */		
		public function get isDisplay():Boolean
		{
			//TODO 未做處理
			return true;
		}
		
		/**
		 * 加载资源。
		 * @param url 资源地址。如果为null则按原来的地址加载。
		 */		
		public function load(url:URLRequest=null):void
		{
			if ( url ) _url = url;
			var load:Loader = new Loader();
			load.contentLoaderInfo.addEventListener(ProgressEvent.PROGRESS, onProgress);
			load.contentLoaderInfo.addEventListener(Event.COMPLETE, onComplete);
			load.load(_url);
		}
		
		/**
		 * 卸载已经加载的资源，节省内存。该方法仅对资源是加载方式提供的实例。
		 */		
		public function unload():void
		{
			if ( _url )
			{
				_source = null;
				bitmapData = null;
				isUpdate = true;
				_isLoaded = false;
			}
		}
		
		/**
		 * 显示的图片数据。
		 * @param cut 是否切割。如果不切割将得到一张整图，需要外部程序自行切割。<br/>
		 * 因为在场景里绘制的时候，也是要再做切割的，所以这里不做切割的话，效率会更高一些。<br/>
		 * 在某些场合，物品有可能不是放在场景里的，所以需要切割后的图片。
		 * @return 图片。
		 */		
		public function displayData(cut:Boolean=false):BitmapData
		{
			var bmpData:BitmapData;
			if ( _source )
			{
				bmpData = new BitmapData(_source.width, _source.height, true, 0);
				if ( !isMirror )
				{
					bmpData.draw(_source);
				}
				else
				{
					bmpData.draw(_source,new Matrix(-1,0,0,1,_source.width));
				}
			}
			else
			{
				bmpData = isMirror ? _mirrorBitmapData : bitmapData;
			}
			if ( cut )
			{
				var viewRect:Rectangle = displayRect(isMirror);
				var cutData:BitmapData = new BitmapData(viewRect.width, viewRect.height, true, 0);
				cutData.copyPixels(bmpData, viewRect, new Point());
				return cutData;
			}
			return bmpData;
		}
		
		/**
		 * 显示区域。
		 */
		public function displayRect(mirror:Boolean):Rectangle
		{
			var quondamRect:Rectangle = _actions.currentFrame.rect
			if ( !mirror ) return quondamRect;
			var rect:Rectangle = new Rectangle();
			if ( _source )
			{
				rect.x = _source.width - quondamRect.x - quondamRect.width;
			}
			else
			{
				rect.x = bitmapData.width - quondamRect.x - quondamRect.width;
			}
			rect.y = quondamRect.y;
			rect.width = quondamRect.width;
			rect.height = quondamRect.height;
			return rect;
		}
		
		/**
		 * 改变动作。
		 * @param actName 动作名。
		 * @param keepStep 是否保持原来的指针位置。
		 */		
		public function changeAct(actName:String, keepStep:Boolean=false):void
		{
			var act:Act = _actions.getActByName(actName);
			if ( act )
			{
				_actions.currentAct = act;
				isUpdate = true;
				if ( keepStep )
				{
					if ( _actions.pointer>=_actions.currentAct.length )
					{
						_actions.pointer = _actions.currentAct.length-1;
					}
				}
				else
				{
					_actions.pointer = 0;
				}
			}
		}
	
		private function onProgress(evt:ProgressEvent):void
		{
			dispatchEvent(evt);
		}
		
		protected function onComplete(evt:Event):void
		{
			var load:Loader = (evt.currentTarget as LoaderInfo).loader;
			load.removeEventListener(ProgressEvent.PROGRESS, onProgress);
			load.removeEventListener(Event.COMPLETE, onComplete);
			if ( load.content is Bitmap )
			{
				bitmapData = (load.content as Bitmap).bitmapData;
				_source = null;
				checkMirror();
			}
			else
			{
				_source = load.content;
			}
			isUpdate = true;
			_isLoaded = true;
			dispatchEvent(new Event(Event.COMPLETE));
		}
		
		protected function onEnterFrame(evt:Event):void
		{
			if ( actions )
			{
				isUpdate = _isUpdate || actions.step();
			}
		}
		
		private function checkMirror():void
		{
			if ( bitmapData && _actions.hasMirrorFrame() )
			{
				_mirrorBitmapData = new BitmapData(bitmapData.width, bitmapData.height,true,0);
				_mirrorBitmapData.draw(bitmapData, new Matrix(-1,0,0,1,bitmapData.width));
			}
		}

	}
}