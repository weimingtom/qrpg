<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute"
	backgroundGradientAlphas="[1.0, 1.0]" backgroundGradientColors="[#A6D95D, #7DD36C]" 
	creationComplete="init()">
	<mx:Script>
		<![CDATA[
			import flash.utils.getTimer;
			
			import mx.events.FlexEvent;
			
			import qrpg.action.Actions;
			import qrpg.display.DisplayGameObject;
			import qrpg.display.RPGRole;
			import qrpg.event.GameEvent;
			
			private var roleXMLPath:String = "assets/knight.xml";
			private var roleImgPath:String = "assets/knight.png";
			
			private var role:RPGRole;
			
			private var roleRect:Rectangle;
			
			private function init():void
			{
				var load:URLLoader = new URLLoader();
				load.load(new URLRequest(roleXMLPath));
				load.addEventListener(Event.COMPLETE, roleXMLCompleteHandler);
			}
			
			private function roleXMLCompleteHandler(evt:Event):void
			{
				(evt.currentTarget as URLLoader).removeEventListener(Event.COMPLETE, roleXMLCompleteHandler);
				var xml:XML = new XML((evt.currentTarget as URLLoader).data);
				var acts:Actions = Actions.createByXML(XML(xml.actions));
				
				role = new RPGRole();
				role.speed = 10;
				role.x = 100;
				role.y = 100;
				role.moveTo(role.x, role.y);
				role.source = roleImgPath;
				role.actions = acts;
				role.addEventListener(Event.COMPLETE, roleCompleteHandler);
			}
			
			private function roleCompleteHandler(evt:Event):void
			{
				role.removeEventListener(Event.COMPLETE, roleCompleteHandler);
				var bmpData:BitmapData = new BitmapData(stage.stageWidth, stage.stageHeight, true, 0);
				img.source = new Bitmap(bmpData);
				
				role.addEventListener(GameEvent.UPDATE, roleUpdateHandler);
				addEventListener(Event.ENTER_FRAME, loop);
				addEventListener(MouseEvent.CLICK, clickHandler);
				addEventListener(Event.RESIZE, resizeHandler);
			}
			
			private function roleUpdateHandler(evt:Event):void
			{
				var bmp:BitmapData = (img.source as Bitmap).bitmapData
				var pic:BitmapData = role.displayData();
				var rect:Rectangle = role.displayRect;
				var dest:Point = role.displayPoint;
				if ( roleRect ) bmp.fillRect(roleRect,0);
				bmp.copyPixels(pic, rect, dest, null, null, false); 
				roleRect = rect.clone();
				roleRect.x = dest.x;
				roleRect.y = dest.y;
				role.isUpdate = false;
			}
			
			private function clickHandler(evt:MouseEvent):void
			{
				role.moveTo(mouseX, mouseY);
			}
			
			private function loop(evt:Event):void
			{
				role.step();
			}
			
			private function resizeHandler(evt:Event):void
			{
				(img.source as Bitmap).bitmapData = new BitmapData(stage.stageWidth, stage.stageHeight, true, 0);
			}
			
		]]>
	</mx:Script>
	<mx:Image id="img"/>
</mx:Application>
