<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" 
	width="400" height="240" title="新建场景" fontSize="12" 
	showCloseButton="true" close="end()">
	<mx:Script>
		<![CDATA[
			import editor.event.EditEvent;
			import mx.core.Application;
			import mx.managers.PopUpManager;
			
			private static var _win:CreateEmptyScene
			
			public static function open():CreateEmptyScene
			{
				if ( !_win ) _win = new CreateEmptyScene();
				if ( !_win.isPopUp )
				{
					PopUpManager.addPopUp(_win, Application.application as DisplayObject, true);
					PopUpManager.centerPopUp(_win);
				}
				return _win;
			}
			
			private function end():void
			{
				PopUpManager.removePopUp(_win);
			}
			
			private function selectFilePath():void
			{
				var file:File = new File();
				file.browseForSave("保存");
				file.addEventListener(Event.SELECT, filePathSelectedHandler);
			}
			
			private function selectPicPath():void
			{
				var file:File = new File();
				file.browseForOpen("场景背景图", [new FileFilter("图片(.jpg, .jpeg, .gif, .png)","*.jpg;*.jpeg;*.gif;*.png")]);
				file.addEventListener(Event.SELECT, picPathSelectedHandler);
			}
			
			private function addScene():void
			{
				if ( filePath.text=="" || picPath.text=="" || widthPic.text=="" || heightPic.text=="" || picWidth.text=="" || picHeight.text=="" ) return;
				
				var arr:Array = filePath.text.split("\\");
				arr.pop();
				var path:String = arr.join("\\")+"\\";
				var src:String = picPath.text.split(path).join("");
				var xml:XML = <scene width={widthPic.text} height={heightPic.text} nodewidth={picWidth.text} nodeheight={picHeight.text} filesrc={src}>
										<unwalk/>
										<bg/>
										<main/>
										<front/>
									</scene>
				
				var file:File = new File(filePath.text);
				var stream:FileStream = new FileStream();
				try
				{
					stream.open(file, FileMode.WRITE);
					stream.writeUTFBytes("<?xml version=\"1.0\" encoding=\"utf-8\"?>\n");
					stream.writeUTFBytes(xml.toXMLString());
					stream.close();
					end();
					new EditEvent(EditEvent.NEW_SCENE, file.nativePath).dispatch();
				}
				catch ( e:* )
				{
					
				}
			}
			
			private function filePathSelectedHandler(evt:Event):void
			{
				filePath.text = (evt.target as File).nativePath;
			}
			
			private function picPathSelectedHandler(evt:Event):void
			{
				picPath.text = (evt.target as File).nativePath;
			}
			
		]]>
	</mx:Script>
	<mx:Label x="10" y="10" text="保存地址"/>
	<mx:TextInput x="71" y="8" editable="false" width="239" id="filePath"/>
	<mx:Button x="318" y="8" label="选择" click="selectFilePath()"/>
	<mx:Label x="10" y="40" text="背景图片"/>
	<mx:TextInput x="71" y="38" editable="false" width="239" id="picPath"/>
	<mx:Button x="318" y="38" label="选择" click="selectPicPath()"/>
	<mx:Label x="20" y="87" text="图片X方向数量"/>
	<mx:TextInput x="113" y="85" width="60" id="widthPic"/>
	<mx:Label x="207" y="87" text="图片Y方向数量"/>
	<mx:TextInput x="300" y="85" width="60" id="heightPic"/>
	<mx:Label x="20" y="117" text="图片宽"/>
	<mx:TextInput x="113" y="115" width="60" text="256" id="picWidth"/>
	<mx:Label x="207" y="117" text="图片高"/>
	<mx:TextInput x="300" y="115" width="60" text="256" id="picHeight"/>
	<mx:Button x="164" y="164" label="添加" click="addScene()"/>
</mx:TitleWindow>
